SUBROUTINE DICT_OP_STATUS(RESULT)
*
* ROUTINE IS USED BY THE I-TYPE DICTIONARY ITEM OP_STATUS
* FOR FILE ORDER_PARENT
* DETERMINES ORDER STATUS (O = OPEN, C = CLOSED)
* BY CHECKING THE INDIVIDUAL STATUS OF EACH ASSOCIATED
* ORDER_CHILD RECORD
* DICT ORDER_PARENT OP_STATUS
* 01: I
* 02: SUBR("DICT_OP_STATUS")
* 03:
* 04: ORDER STATUS
* 05: 3L
* 06: S

*
$INCLUDE NXT.INCLUDES ORDER_PARENT.H
$INCLUDE NXT.INCLUDES ORDER_CHILD.H
*
RESULT = "C"
OPEN "ORDER_CHILD" TO CHILD.FB THEN
  CHILD.COUNT = DCOUNT(@RECORD<OP_CHILD>,@VM)
  FOR I = 1 TO CHILD.COUNT
    TEST.CHILD.ID = @ID:'*':I
    READ TEST.CHILD.REC FROM CHILD.FB, TEST.CHILD.ID THEN
      IF TEST.CHILD.REC<OC_CLOSE> # "C" THEN   ;* TEST FOR MANUALLY CLOSED
        CHILD.STATUS = TEST.CHILD.REC<OC_ITEM_STAT>
        IF CHILD.STATUS = "I" OR CHILD.STATUS = "" THEN
          RESULT = "O"
          EXIT
        END
      END
    END
  NEXT I
END ELSE
  RESULT = -1
END
RETURN
END
