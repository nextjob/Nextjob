SUBROUTINE DATE.PICKER(PASS.DATE,GUISTATE,GUI.PROJECT.FILE)
************************************************************
* AccuTerm GUI Application Subroutine Skeleton
*
* Date selection subroutine, clicking on a date passes back
* a valid Pick style internal date via the PASS.DATE variable
* or else a Null is returned on a bail out.
*
* Alan Gruskoff - Performant Systems, september 2004
************************************************************
$INCLUDE NXT.INCLUDES NXT.COMMON
*
GUIAPP = "DATE.PICKER"
GUIFRM = "Form1"
APP.TITLE = "Date Picker"
PASS.DATE = ""
*
*-->BEGIN GUI HEADER<--*
$INCLUDE GUIBP ATGUIEQUATES
* Add your equates here...
TODAY = OCONV(DATE(),"D4-")
CURR.DAY   = FIELD(TODAY,"-",2)
CURR.MONTH = FIELD(TODAY,"-",1)
CURR.YEAR  = FIELD(TODAY,"-",3)
**
SVM = CHAR(252); VM = CHAR(253); AM = CHAR(254)
ESC = CHAR(27) ; STX = CHAR(2)
EQUATE WHITE TO 16777215, BLACK TO 0, RED TO 255
SAVE.ROW = 0; SAVE.COL = 0
*
*-->END GUI HEADER<--*
*
*
************************************************************
*
*
*-->BEGIN GUI STARTUP<--*
ERR.MSG = ""
READ TEMPLATE FROM GUI.PROJECT.FILE,'DATE.PICKER' ELSE ERR.MSG = "There is no DATE.PICKER template"
CALL ATGUIRUNMACRO(TEMPLATE,'',GUIERRORS,GUISTATE)
IF GUIERRORS<1> >= 3 THEN ERR.MSG = "GUIERRORS = ":GUIERRORS<1>
CALL ATGUISHOW(GUIAPP,GUIFRM,'','',GUIERRORS,GUISTATE)
IF GUIERRORS<1> >= 2 THEN ERR.MSG = "GUIERRORS = ":GUIERRORS<1>
IF ERR.MSG NE "" THEN
  GOSUB SHOW.ERR.MSG
  GOSUB GUI.DATE.PICKER.FORM1.CLOSE
  RETURN
END
**
BASE.DATE = CURR.MONTH"R%2":"-01-":CURR.YEAR
GOSUB RECALC.DATE
*-->END GUI STARTUP<--*
*
*
************************************************************
*
*
*-->BEGIN EVENT LOOP<--*
GUIDONE = 0
LOOP UNTIL GUIDONE DO
  CALL ATGUIWAITEVENT(GUIAPP,GUIFRM,GUICTL,GUIEVT,GUIARGS,GUIERRORS,GUISTATE)
  IF GUIERRORS<1> >= 2 THEN ERR.MSG = "GUIERRORS = ":GUIERRORS<1>; GOSUB SHOW.ERR.MSG
  GUIAPP=OCONV(GUIAPP,'MCU')
  GUIFRM=OCONV(GUIFRM,'MCU')
  GUICTL=OCONV(GUICTL,'MCU')
  GOSUB GUI.DECODE.EVENT
REPEAT
*-->END EVENT LOOP<--*
*
*
************************************************************
*
*
*-->BEGIN GUI TRAILER<--*
CALL ATGUIHIDE(GUIAPP,GUIFRM,'','',GUIERRORS,GUISTATE)
CALL ATGUIDELETE(GUIAPP,'','',GUIERRORS,GUISTATE)
RETURN
*-->END GUI TRAILER<--*
*
*
************************************************************
*
*
*-->BEGIN EVENT DECODER<--*
GUI.DECODE.EVENT: *
IF NUM(GUIEVT) THEN
  BEGIN CASE
    CASE GUIAPP='DATE.PICKER'
      BEGIN CASE
        CASE GUIFRM='FORM1'
          BEGIN CASE
            CASE GUICTL=''
              BEGIN CASE
                CASE GUIEVT=GECLOSE
                  GOSUB GUI.DATE.PICKER.FORM1.CLOSE;GUIEVT=0
              END CASE
            CASE GUICTL='COMMAND.MONTH.BWD'
              BEGIN CASE
                CASE GUIEVT=GECLICK
                  GOSUB GUI.DATE.PICKER.FORM1.COMMAND.MONTH.BWD.CLICK;GUIEVT=0
              END CASE
            CASE GUICTL='COMMAND.MONTH.FOR'
              BEGIN CASE
                CASE GUIEVT=GECLICK
                  GOSUB GUI.DATE.PICKER.FORM1.COMMAND.MONTH.FOR.CLICK;GUIEVT=0
              END CASE
            CASE GUICTL='DATE.GRID'
              BEGIN CASE
                CASE GUIEVT=GEDBLCLICK
                  GOSUB GUI.DATE.PICKER.FORM1.DATE.GRID.DBLCLICK;GUIEVT=0
              END CASE
          END CASE
      END CASE
  END CASE
  *IF GUIEVT THEN
  ** Unhandled event - may be dynamic
  *IF GUIAPP='DATE.PICKER' THEN
  *GOSUB GUI.DYNAMIC.EVENTS
  *GUIEVT=0
  *END
  *END
  *END ELSE
  *IF GUIAPP='DATE.PICKER' THEN
  *GOSUB GUI.CUSTOM.EVENTS
  *GUIEVT=0
  *END
END
RETURN
*-->END EVENT DECODER<--*
*
*
************************************************************
*
*
*-->BEGIN CLOSE EVENT HANDLER<--*
GUI.DATE.PICKER.FORM1.CLOSE: *
* Default form close event handler
GUIDONE = 1
RETURN
*-->END CLOSE EVENT HANDLER<--*
*
*
************************************************************
*
*
*-->BEGIN EVENT HANDLER<--*
GUI.DATE.PICKER.FORM1.COMMAND.MONTH.BWD.CLICK: *
CURR.MONTH = CURR.MONTH - 1
IF CURR.MONTH = 0 THEN CURR.MONTH = 12; CURR.YEAR = CURR.YEAR - 1
BASE.DATE = CURR.MONTH"R%2":"-01-":CURR.YEAR
GOSUB RECALC.DATE
RETURN
*-->END EVENT HANDLER<--*
*
*
************************************************************
*
*
*-->BEGIN EVENT HANDLER<--*
GUI.DATE.PICKER.FORM1.COMMAND.MONTH.FOR.CLICK: *
* Add your event code here...
CURR.MONTH = CURR.MONTH + 1
IF CURR.MONTH = 13 THEN CURR.MONTH = 1; CURR.YEAR = CURR.YEAR + 1
BASE.DATE = CURR.MONTH"R%2":"-01-":CURR.YEAR
GOSUB RECALC.DATE
RETURN
*-->END EVENT HANDLER<--*
*
*
************************************************************
*
*
*-->BEGIN EVENT HANDLER<--*
GUI.DATE.PICKER.FORM1.DATE.GRID.DBLCLICK: *
COL = GUIARGS<1,1>; ROW = GUIARGS<1,2>
CALL ATGUIGETVALUES(GUIAPP,GUIFRM,'DATE.GRID',VALUES,GUIERRORS,GUISTATE)
VAL = VALUES<1,ROW,COL>
PASS.DATE = DATE.ARRAY<1,ROW,COL>
GUIDONE = 1
RETURN
*-->END EVENT HANDLER<--*
*
*
************************************************************
*
*
*-->BEGIN ERROR HANDLER<--*
SHOW.ERR.MSG: *
CAPTION = APP.TITLE
STYLE   = 2 ; ** 0=none 1="X"  2="!"  3="?"  4="i"
BUTTONS = 0 ; ** 0 = OK only
HELPID  = ""
CALL ATGUIMSGBOX(ERR.MSG, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS, GUISTATE)
RETURN
*-->END ERROR HANDLER<--*
*
*
************************************************************
*
*
RECALC.DATE: *
IBASE.DATE = ICONV(BASE.DATE,"D4-")
GUICTLS = ""; VALUES = ""
GUICTLS<1> = "MONTH.Label" ;  VALUES<1> = OCONV(IBASE.DATE,"DMA")
GUICTLS<2> = "YEAR.Label"  ;  VALUES<2> = OCONV(IBASE.DATE,"DY")
CALL ATGUILOADVALUES(GUIAPP,GUIFRM,GUICTLS,VALUES,GUIERRORS,GUISTATE)
**
DATE.ARRAY = ""; DATE.GRID = ""
TODAYS.ROW = 0; TODAYS.COL = 0
DAY.OF.MONTH = 0; END.OF.MONTH = 0
ROW = 1
COL = OCONV(IBASE.DATE,"DW") ;** Day of the week index (MON=1,SUN=7)
COL = COL + 1
IF COL = 8 THEN COL = 1
LOOP
  DAY.OF.MONTH = DAY.OF.MONTH + 1
  IF DAY.OF.MONTH = 32 THEN
    END.OF.MONTH = 1
  END ELSE
    TEST.DATE = CURR.MONTH"R%2":"-":DAY.OF.MONTH:"-":CURR.YEAR
    *      IF ICONV(TEST.DATE,"D4-") = "" THEN END.OF.MONTH = 1 ;* QM MODE
    TEST.MONTH = OCONV(ICONV(TEST.DATE,"D"),"DM")
    IF TEST.MONTH # CURR.MONTH THEN END.OF.MONTH = 1
  END
UNTIL END.OF.MONTH DO
  IDATE = IBASE.DATE + DAY.OF.MONTH - 1 ;** the internal date value
  DATE.ARRAY<1,ROW,COL> = IDATE
  DATE.GRID<1,ROW,COL>  = DAY.OF.MONTH  ;** the day of the month
  IF OCONV(IDATE,"D4-") = TODAY THEN
    TODAYS.ROW = ROW; TODAYS.COL = COL
  END
  COL = COL + 1
  IF COL = 8 THEN COL = 1; ROW = ROW + 1
REPEAT
**
CALL ATGUILOADVALUES(GUIAPP,GUIFRM,"DATE.GRID",DATE.GRID,GUIERRORS,GUISTATE)
CALL ATGUISETPROP(GUIAPP,GUIFRM,"DATE.GRID",GPCOLUMN,0,0,0,GUIERRORS,GUISTATE)
CALL ATGUISETPROP(GUIAPP,GUIFRM,"DATE.GRID",GPROW,0,0,0,GUIERRORS,GUISTATE)
**
IF SAVE.ROW NE 0 THEN
  CALL ATGUISETPROP(GUIAPP,GUIFRM,"DATE.GRID",GPFORECOLOR,SAVE.COL,SAVE.ROW,BLACK,GUIERRORS,GUISTATE)
  CALL ATGUISETPROP(GUIAPP,GUIFRM,"DATE.GRID",GPBACKCOLOR,SAVE.COL,SAVE.ROW,WHITE,GUIERRORS,GUISTATE)
  SAVE.ROW = 0; SAVE.COL = 0
END
IF TODAYS.ROW NE 0 THEN
  CALL ATGUISETPROP(GUIAPP,GUIFRM,"DATE.GRID",GPFORECOLOR,TODAYS.COL,TODAYS.ROW,WHITE,GUIERRORS,GUISTATE)
  CALL ATGUISETPROP(GUIAPP,GUIFRM,"DATE.GRID",GPBACKCOLOR,TODAYS.COL,TODAYS.ROW,RED,GUIERRORS,GUISTATE)
  SAVE.ROW = TODAYS.ROW; SAVE.COL = TODAYS.COL
END
**
RETURN
*
*
************************************************************
END
