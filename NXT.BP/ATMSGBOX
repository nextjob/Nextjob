SUBROUTINE ATMSGBOX(TITLE, TEXT, STYLE, RESPONSE)
*
* ACCUTERM 2K2 MESSAGE BOX USING SCRIPT
* 
*  CALL THIS SUBROUTINE TO DISPLAY A MESSAGE BOX AND RETURN
*  WHICH BUTTON THE USER CLICKED TO DISMISS THE DIALOG.
*  TEXT CAN CONTAIN MULTIPLE LINES, SEPARATED BY ATTRIBUTE
*  MARKS. STYLE IS FORMED BY ADDING CONSTANTS FROM THE
*  BUTTONS, DEFAULT BUTTON AND ICON GROUPS.
*
*  BUTTONS:
*   OK = 0
*   OK/Cancel = 1
*   Abort/Retry/Ignore = 2
*   Yes/No/Cancel = 3
*   Yes/No = 4
*   Retry/Cancel = 5
*
*  DEFAULT BUTTON:
*   First Button = 0
*   Second Button = 256
*   Third Button = 512
*
*  ICON:
*   No icon = 0
*   Stop icon = 16
*   Question icon = 32
*   Attention icon = 48
*   Information icon = 64
*
*  RESPONSE:
*   OK = 1
*   Cancel = 2
*   Abort = 3
*   Retry = 4
*   Ignore = 5
*   Yes = 6
*   No = 7
*
*  NOTE: THIS SUBROUTINE BUILDS AND EXECUTES A SCRIPT WHICH
*  DISPLAYS THE MESSAGE BOX, THEN TRANSMITS THE RESULTS BACK
*  TO THE HOST.
*
EQU ESC TO CHAR(27), STX TO CHAR(2), CR TO CHAR(13), EM TO CHAR(25)
EQU AM TO CHAR(254)
*Build script to display message box
SCR='On Error Resume Next'
SCR=SCR:EM:'Dim txt As String'
SCR=SCR:EM:'Dim rc As Integer'
N=DCOUNT(TEXT,AM)
FOR I=1 TO N
 ARG=TEXT<I>; GOSUB 100
 LOOP WHILE LEN(ARG) > 250 DO
  SCR=SCR:EM:'txt = txt & "':ARG[1,250]:'"'
  ARG=ARG[251,999999]
 REPEAT
 SCR=SCR:EM:'txt = txt & "':ARG:'"'
 IF I<N THEN SCR=SCR:EM:'txt = txt & vbCrLf'
NEXT I
*Display the message box
SCR=SCR:EM:'rc = MsgBox(txt,':STYLE:',"':TITLE:'")'
*Transmit results to host
SCR=SCR:EM:'InitSession.Activate'
SCR=SCR:EM:'InitSession.Output CStr(rc) & vbCr'
*Execute script
PRINT ESC:STX:'P':SCR:CR:
*Retrieve the result
PROMPT ''
ECHO OFF
INPUT RESPONSE:
ECHO ON
PROMPT '?'
RETURN
*
100 * Local subroutine to fixup embedded double-quote marks
K = 1
LOOP
   J = INDEX(ARG, '"', K)
WHILE J DO
   ARG = ARG[1, J] : ARG[J, 99999]
   K = K + 2
REPEAT
RETURN
*
END
