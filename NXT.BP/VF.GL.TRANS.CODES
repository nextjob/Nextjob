SUBROUTINE VF.GL.TRANS.CODES(VF_REC,STATUS,GUIERRORS,GUISTATE)
!
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: VF.DIALOG
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 00/00/2014
* UPDATED:
*
* MAINTENANCE RECORD:
*
*
* EQUATE:
*
*
* VARIABLES:
* VF_REC - VALIDATAION_FILE RECORD TO VALIDATE IN THIS CASE IT WILL BE GL.TRANS.CODES
*
* FILES:
*
*
* DESIGN DESCRIPTION:
* ROUTINE IS CALLED TO VALIDATE THE GL.TRANS.CODES AGAINST THE CHART OF ACCOUNTS RECORD.
* ROUTINE CHECK TO SEE THAT ALL ACCOUNTS ENTERED ON GL.TRANS.CODES ARE FOUND IN SYSTEM_FILE CHART_OF_ACCOUNTS
*
!
** INCLUDE STANDARD VARIABLE, EQUATES
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES SYSTEM_FILE.EQU
$INCLUDE NXT.INCLUDES VALIDATION_FILE.H
*
PROG_ID   = "VF.GL.TRANS.CODES"
GUIERRORS = ''
GET_REC_MSG_LIST = ''
*
STATUS = ''
*
CALL GET.RECORD("SYSTEM_FILE",C.SYSTEM_FILE,"CHART_OF_ACCOUNTS",COA_REC,@FALSE,ERROR_STATUS,GET_REC_MSG_LIST,GUIERRORS,GUISTATE)
IF COA_REC = '' THEN
  LOG.OPTS  = "DISPLAY"
  ERROR_NBR = ERR_SYS
  ERROR_MSG =  "Missing CHART_OF_ACCOUNTS from SYSTEM_FILE"
  CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
  STATUS = ERROR_MSG
END ELSE
  ACCTS.CNT = DCOUNT(COA_REC,@AM)              ;* GET COUNT OF GL ACCOUNTS DEFINED IN COA.REC
  VF.CODE.CNT = DCOUNT(VF_REC<CODE_ID>,@VM)    ;* GET COUNT OF VALIDATION ENTRIES
  *
  * LOOK FOR ACCOUNT ON VALIDATION RECORD VAL1 IN COA.REC
  FOR I = 1 TO VF.CODE.CNT
    ACCOUNT.FOUND = @FALSE
    FOR AT = 1 TO ACCTS.CNT
      IF VF_REC<CODE_VAL1,I> = COA_REC<AT,1> THEN
        ACCOUNT.FOUND = @TRUE
        EXIT
      END
    NEXT AT
    IF NOT(ACCOUNT.FOUND) THEN
      STATUS = STATUS :'Code: ':VF_REC<CODE_ID,I> :' Account: ': VF_REC<CODE_VAL1,I> :' Not In COA, '
    END
  NEXT I
  *
  * LOOK FOR ACCOUNT ON VALIDATION RECORD VAL2 IN COA.REC
  FOR I = 1 TO VF.CODE.CNT
    ACCOUNT.FOUND = @FALSE
    FOR AT = 1 TO ACCTS.CNT
      IF VF_REC<CODE_VAL2,I> = COA_REC<AT,1> THEN
        ACCOUNT.FOUND = @TRUE
        EXIT
      END
    NEXT AT
    IF NOT(ACCOUNT.FOUND) THEN
      STATUS = STATUS :'Code: ':VF_REC<CODE_ID,I> :' Account: ': VF_REC<CODE_VAL2,I> :' Not In COA, '
    END
  NEXT I
END
IF STATUS = '' THEN STATUS = 'OK'
RETURN
**********************************************************************************
END
