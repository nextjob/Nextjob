SUBROUTINE CUSTOM.CONTROL.VALIDATE(CFLD_ID,CFLD_FILE_REC,CALLER_GUIARGS,CALLER_RESULT,CALLER_RESULT_FLD,CFLD_OPTIONS,CFLD_MSG,GUIERRORS,GUISTATE)
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: CUSTOM.CONTROL.VALIDATE
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 06/00/2015
* UPDATED:
*
* MAINTENANCE RECORD:
*
* EQUATE:
*
*
* VARIABLES:
*
* ON ENTRY
* CFLD_ID -CUSTOM FIELD CONTROL ID
* CFLD_FILE_REC - CUSTOM FIELDS CONTROL RECORD
* CALLER_GUIARGS - USER ENTERED VALUE
* RETURNS
* CALLER_RESULT<CALLER_RESULT_FLD> - VALIDATED / CONVERTED USER ENTERED VALUE (NOTE DO THIS TO PREVENT VARIABLE PASSED BY VALUE ISSUES)
* CFLD_MSG - MESSAGE PASSED BACK BY CALL TO CUSTOM VALIDATION ROUTINE
*
* FILES:
*
*
* DESIGN DESCRIPTION:
* VALIDATE USER ENTERED DATA BASED ON CUSTOM FIELDS CONTROL RECORD SETTINGS
*
*equate CFLD_LABEL_CNTRL_ID     TO    1  ;* LABEL CONTROL ID
*equate CFLD_LABEL_CAPTION      TO    2  ;* LABEL TEXT
*equate CFLD_TXTBOX_CNTRL_ID    TO    3  ;* TEXT BOX CONTROL ID
*equate CFLD_TXTBOX_DATA_TYPE   TO    4  ;* TEXT BOX DATA TYPE (OPTIONAL)
*equate CFLD_TXTBOX_CONV_CODE   TO    5  ;* CONVERSION CODE FOR DATA CONVERSION (OPTIONAL)
*equate CFLD_TXTBOX_VALIDATE    TO    6  ;* CATALOGED SUBROUTINE NAME OF ROUTINE CALLED TO PERFORM DATA VALIDATION (OPTIONAL)
!
** INCLUDE STANDARD VARIABLE, EQUATES
*
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES SYSTEM_FILE.EQU
$INCLUDE GUIBP ATGUIEQUATES
*
PROG_ID = "CUSTOM.CONTROL.VALIDATE"
*
LOCATE CFLD_ID IN CFLD_FILE_REC<CFLD_TXTBOX_CNTRL_ID,1> SETTING FLD.IDX THEN
   NULL
END ELSE
   FLD.IDX = -1  ;* IF THERE IS A CONTROL RECORD AND THE CONTROL ID IS NOT FOUND THIS IS AN ERROR, CATCH BELOW
END
*
BEGIN CASE
   CASE  CFLD_FILE_REC = ''
      CALLER_RESULT<CALLER_RESULT_FLD> = CALLER_GUIARGS    ;* NO CONVERSION SPECIFIED, SIMPLY RETURN THE VALUE

   CASE FLD.IDX < 0
      LOG.OPTS = "DISPLAY"
      ERROR_NBR = ERR_SYS
      ERROR_MSG = CFLD_ID:", Not Found In Passed Custom Field Control Record"
      CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
      CALLER_RESULT<CALLER_RESULT_FLD> = CALLER_GUIARGS    ;* NO CONVERSION SPECIFIED, SIMPLY RETURN THE VALUE

   CASE CFLD_FILE_REC<CFLD_TXTBOX_VALIDATE,FLD.IDX> # ''   ;* IS THERE A VALIDATION ROUTINE DEFINED?
      SUB.NAME = CFLD_FILE_REC<CFLD_TXTBOX_VALIDATE,FLD.IDX>
      CALL !FINDPROG(RESULT,SUB.NAME)   ;* IS THIS A VALID SUBROUTINE NAME?
      IF RESULT = '' THEN
         LOG.OPTS = "DISPLAY"
         ERROR_NBR = ERR_SYS
         ERROR_MSG =  "Reference Validation Routine: ":SUB.NAME:" Defined In Custom Field Control Record Not Found!"
         CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
         CALLER_RESULT<CALLER_RESULT_FLD> = CALLER_GUIARGS
      END ELSE
         CALL @SUB.NAME(VALIDATED_RESULT,VALID_RESULT,CFLD_MSG,CALLER_GUIARGS,GUIERRORS,GUISTATE)  ;* CALL THE CUSTOM VALIDATION ROUTINE
  *
  *       NOTE - HAVE YET TO WRITE A CUSTOM VALIDATE ROUTINE, HOOKS READY, NEED TO TEST
  *            - ALSO A CUSTOM VALIDATE ROUTINE COULD CALL OR CREATE A CUSTOM DIALOG TO ALLOW FOR ADDITONAL
  *            - DATA SELECTION / ENTRY, ONLY ISSUE IS CALLING FORM IS CURRENTLY LIMITED TO A TEXT BOX DISPLAY
  *            - BUT YOU COULD HAVE THE CUSTOM VALIDATE ROUTINE DISPLAY A DROP DOWN LIST WITH THE SELECTED
  *            - VALUE BEING RETURNED, THIS HOWEVER WOULD IDEALLY REQUIRE THE CALLING FORM TO HAVE A CLICK EVENT ON THE OBJECT
  *            - NOT THE VALIDATE EVENT??   
  *
  
         IF VALID_RESULT THEN
            IF CFLD_FILE_REC<CFLD_TXTBOX_CONV_CODE,FLD.IDX> = '' THEN  ;* IS THERE A CONVERSION TO PERFORM?
               CALLER_RESULT<CALLER_RESULT_FLD> =  VALIDATED_RESULT
            END ELSE
               CALLER_RESULT<CALLER_RESULT_FLD> = ICONV(VALIDATED_RESULT,CFLD_FILE_REC<CFLD_TXTBOX_CONV_CODE,FLD.IDX>)
            END
         END
      END

   CASE CFLD_FILE_REC<CFLD_TXTBOX_CONV_CODE,FLD.IDX> # ''   ;* IS THERE A CONVERSION CODE?
      CALLER_RESULT<CALLER_RESULT_FLD> = ICONV(CALLER_GUIARGS,CFLD_FILE_REC<CFLD_TXTBOX_CONV_CODE,FLD.IDX>)
   CASE 1
      CALLER_RESULT<CALLER_RESULT_FLD> =  CALLER_GUIARGS
END CASE
RETURN
************************************************************
*
**-->BEGIN ERROR HANDLER<--*
GUI.ERROR: *
GUI.DLG.EXIT = 1
LOG.OPTS = "DISPLAY"
ERROR_NBR = ERR_GUI
ERROR_MSG =  ERR_MESSAGE(ERR_GUI)
CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
RETURN
*-->END ERROR HANDLER<--*
*
*
************************************************************
END
