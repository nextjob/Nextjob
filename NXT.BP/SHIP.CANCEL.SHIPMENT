SUBROUTINE SHIP.CANCEL.SHIPMENT(DMMY,OC_ID,OC_REC,SHIP.CAN,GUIERRORS,GUISTATE)
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: SHIP.CANCEL.SHIPMENT
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 00/00/2007
* UPDATED:
*
* MAINTENANCE RECORD:
*
* EQUATE:
*
*
* VARIABLES:
* ON ENTRY
* DMMY - NOT USED
* OC_ID   - ORDER CHILD ID
* OC_REC  - ORDER CHILD RECORD
*
* ON EXIT:
* SALES RECORD FOR LAST SHIPMENT DELETE
* CERTS RECORD FOR LAST SHIPMENT DELETE
* OC_REC - ORDER CHILD UPDATED WITH LAST SHIPMENT REMOVED
* SHIP.CAN - @TRUE SHIPMENT WAS CANCELLED
*          - @FALSE SHIPMENT WAS NOT CANCELLED
*
* FILES:
*
*
* DESIGN DESCRIPTION:
*
** INCLUDE STANDARD VARIABLE, EQUATES
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES ORDER_CHILD.H
$INCLUDE NXT.INCLUDES ORDER_PARENT.H
$INCLUDE NXT.INCLUDES SALES.H
$INCLUDE NXT.INCLUDES PARTS.H
$INCLUDE NXT.INCLUDES CERTS.H
*
PROG_ID = "SHIP.CANCEL.SHIPMENT"
SHIP.CAN = @FALSE
MSG_LIST = ''
SCHD.COUNT = DCOUNT(OC_REC<OC_SCHD_ID>,@VM)
FOR I = SCHD.COUNT TO 1 STEP -1
   SA_ID = OC_REC<OC_PACK_ID,I>
   IF SA_ID # '' THEN
      CALL GET.RECORD("SALES",C.SALES,SA_ID,SA_REC,@FALSE,ERROR_STATUS,MSG_LIST,GUIERRORS,GUISTATE)
      IF ERROR_STATUS = '' THEN
         IF SA_REC<SA_INVOICE_ID> # ''  AND NX_AR_ACTIVE THEN
            PRMPT = "Pack Slip: ":SA_ID:" invoiced, not allow (delete invoice first)"
            CAPTION = "Order Entry"
            STYLE   = 2  ;* !
            BUTTONS = 0  ;* ok
            HELPID = ""
            CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
            GOTO 9999
         END ELSE
            PACK.ID = OC_REC<OC_PACK_ID,I>
            CERT.ID = OC_REC<OC_CERT_ID,I>
*
* GET CONFIRMATION FROM USER
            PRMPT = "Cancel packslip:":PACK.ID:"?"
            CAPTION = "Order Entry"
            STYLE   = 2  ;* !
            BUTTONS = 4  ;* yes / no
            HELPID = ""
            CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
            IF RESPONSE = 6 THEN  ;* YES
*
* DELETE PACKSLIP
*
               DELETE C.FILE.VAR(C.SALES), PACK.ID
*
* DELETE CERT
*
               DELETE C.FILE.VAR(C.CERTS), CERT.ID
*
* REMOVE FROM ORDER CHILD
*
TEST.AGAIN:
               LOCATE PACK.ID IN OC_REC<OC_PACK_ID,1> SETTING IDX THEN
                  DEL OC_REC<OC_DEL_DATE,IDX>
                  DEL OC_REC<OC_DEL_QTY,IDX>
                  DEL OC_REC<OC_PACK_ID,IDX>
                  DEL OC_REC<OC_CERT_ID,IDX>
                  GOTO TEST.AGAIN
               END
*
               OC_REC<OC_ITEM_STAT> = "I"   ;* RESET ORDER_CHILD STATUS
               CALL WRITE.RECORD(OC_ID,OC_REC,C.ORDER_CHILD,@TRUE,ERROR_STATUS)
*
* KEEP TRACK OF CANCELLED PACK SLIPS
               LOG.OPTS = ""
               ERROR_NBR = ERR_NOTE
               ERROR_MSG = 'Packslip: ':PACK.ID:' Cancelled by user: ':@LOGNAME
               CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
*
               SHIP.CAN = @TRUE
            END
            GOTO 9999
*
         END
      END ELSE
         PRMPT = "Unable to access sales record ":SA_ID:" reason ":ERROR_STATUS
         CAPTION = "Order Entry"
         STYLE   = 2  ;* !
         BUTTONS = 0  ;* ok
         HELPID = ""
         CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
      END
   END
NEXT I
*
9999: *
RETURN
END
