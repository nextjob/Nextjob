SUBROUTINE DICT_PO_STATUS(RESULT)
*
* ROUTINE IS USED BY THE I-TYPE DICTIONARY ITEM PO_STATUS
* FOR FILE PO_PARENT
* DETERMINES ORDER STATUS (O = OPEN, C = CLOSED)
* BY CHECKING THE INDIVIDUAL STATUS OF EACH ASSOCIATED
* PO_CHILD RECORD
* DICT PO_PARENT PO_STATUS
* 01: I
* 02: SUBR("DICT_PO_STATUS")
* 03:
* 04: ORDER STATUS
* 05: 3L
* 06: S

*
$INCLUDE NXT.INCLUDES PO_PARENT.H
$INCLUDE NXT.INCLUDES PO_CHILD.H
*
RESULT = "C"
OPEN "PO_CHILD" TO CHILD.FB THEN
  CHILD.COUNT = DCOUNT(@RECORD<PO_CHILD>,@VM)
  FOR I = 1 TO CHILD.COUNT
    TEST.CHILD.ID = @ID:'*':I
    READ TEST.CHILD.REC FROM CHILD.FB, TEST.CHILD.ID THEN
      IF TEST.CHILD.REC<PC_CLOSE> # "C" THEN   ;* TEST FOR MANUALLY CLOSED
        CHILD.STATUS = TEST.CHILD.REC<PC_ITEM_STAT>
        IF CHILD.STATUS = "I" OR CHILD.STATUS = "" THEN
          RESULT = "O"
          EXIT
        END
      END
    END
  NEXT I
END ELSE
  RESULT = -1
END
RETURN
END
