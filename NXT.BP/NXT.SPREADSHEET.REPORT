SUBROUTINE NXT.SPREADSHEET.REPORT(SELECT.STR,FILE.NAME,PRMPT.OPEN,OPTIONS, GUIERRORS, GUISTATE)
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: NXT.SPREADSHEET.REPORT
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 00/00/2014
* UPDATED:
*
* MAINTENANCE RECORD:
* 11/20/14 - WRITE REPORTS TO QMSYS TEMP DIRECTORY THEN TRANSFER TO USERS MY DOCUMENTS FOLDER
*
* ROUTINE IS CALLED TO CREATE SPREADSHEET FILE (CURRENTLY CSV)
* FROM THE PASSED SELECTION STRING
*
* SELECT.STR - SELECTION (SORT) STRING WHICH CREATES CSV REPORT FROM QM FILE
*  EG) SELECT.STR = 'SORT AP_INVOICE BY AP_DUE_DATE WITH AP_BAL_DUE GT 0 INVOICE AP_DUE_DATE AP_INV_DATE AP_VENDOR_NAME AP_INV_AMT TOTAL AP_BAL_DUE'
* FILE.NAME  - NAME OF CSV FILE CREATED
* PRMPT.OPEN - PROMPT TO OPEN FILE (T /F )
* OPTIONS    - N/A AT THIS TIME
*
* FILES:
*
*
* DESIGN DESCRIPTION:
*
!
** INCLUDE STANDARD VARIABLE, EQUATES
*
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES SYSTEM_FILE.EQU
$INCLUDE GUIBP ATGUIEQUATES
*
$INCLUDE SYSCOM KEYS.H
*
PROG_ID = "NXT.SPREADSHEET.REPORT"
GET_REC_MSG_LIST = ''
*
CALL GET.RECORD("SYSTEM_FILE",C.SYSTEM_FILE,"NETWORK_PATH",NETWORK_PATH_REC,@FALSE,ERROR_STATUS,GET_REC_MSG_LIST,GUIERRORS,GUISTATE)
REPORT.SRVR.TEMP.DIR = OCONV(NETWORK_PATH_REC<SYS_SERVER_REPORT_PATH >,"MCU")
REPORT.SHARE.TEMP.DIR = OCONV(NETWORK_PATH_REC<SYS_SHARE_REPORT_PATH >,"MCU")
*
*
* create temp file to recieve report
*
* REPORT.SRVR.TEMP.DIR = OCONV(NETWORK_PATH_REC<SYS_SERVER_REPORT_PATH >,"MCU")
* REPORT.SHARE.TEMP.DIR = OCONV(NETWORK_PATH_REC<SYS_SHARE_REPORT_PATH >,"MCU")
CALL SHARE.FILE.PATH.CALC(REPORT.SHARE.TEMP.DIR,NETWORK_PATH_REC,SHARE.TEMP.DIR,'EXPAND')
*
TEMP.DIR = REPORT.SRVR.TEMP.DIR
TEMP.FILE= "_":SYSTEM(19)   ;* using QM TEMP FILE, FILE NAME REQUIRES "_" PREFIX TO PREVENT CONFLICT WITH QM SYS TEMP FILE NAMES
OPENPATH TEMP.DIR TO TEMP.DIR.FV THEN

   REPORT.PATH = TEMP.DIR:@DS:TEMP.FILE
   SHARE.REPORT.PATH = SHARE.TEMP.DIR:@DS:TEMP.FILE

   OS.SENT = SELECT.STR : ' ID-SUP HDR.SUP CSV TO ':REPORT.PATH:' NO.QUERY'
   EXECUTE OS.SENT TRAPPING ABORTS CAPTURING DMMY

   IF @ABORT.CODE = 0 THEN
      *
      *
      * TRANSFER TO USER MY DOCUMENTS
      *
      CALL ATGETDOCPATH(MYDOCUMENTS_PATH)
      COPY_TO_FILE_LOC = MYDOCUMENTS_PATH:@DS:FILE.NAME
      CALL GUI.FILE.COPY(SHARE.REPORT.PATH,COPY_TO_FILE_LOC,STATUS)
      IF STATUS = '' THEN
         *
         * CALL UP REPORT
         *
         CRT ESC:STX:"<":COPY_TO_FILE_LOC
      END ELSE
         PRMPT = "Unable to copy Report to MY DOCUMENTS, reason ":STATUS
         CAPTION = PROG_ID
         STYLE   = 2  ;* !
         BUTTONS = 0  ;* ok
         HELPID = ""
         CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
      END
      *
      * GET RID OF THE FILES STORED IN THE TEMP DIR
      *
      DELETE TEMP.DIR.FV, TEMP.FILE
      CLOSE TEMP.DIR.FV
   END ELSE
      IF @ABORT.CODE = 3 THEN
         PRMPT = "Report Creation, failed,: ":@ABORT.CODE:" ":FILE.NAME:" Is the file in use by another program?"
      END ELSE
         PRMPT = "Report Creation, failed,: ":@ABORT.CODE:' ':@ABORT.MESSAGE
      END
      *
      CAPTION = PROG_ID
      STYLE   = 2  ;* !
      BUTTONS = 0  ;* ok only
      HELPID = ""
      CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
   END
   *
END ELSE
   LOG.OPTS  = "DISPLAY"
   ERROR_NBR = ERR_SYS
   ERROR_MSG =  "Missing NETWORK_PATH record in SYSTEM_FILE"
   CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
END
RETURN
END

