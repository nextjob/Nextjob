SUBROUTINE WRITE.GLT(GL_CODE,DB_AMT,CR_AMT,EFF_DATE,SUBSYS,REF_ID,GUIERRORS,GUISTATE)
!
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: WRITE.GLT
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 00/00/2014
* UPDATED:
*
* MAINTENANCE RECORD:
*
* EQUATE:
*
*
* VARIABLES:
*
* GL_CODE - GL TRANSACTION CODE AS DEFINED IN VALIDATION FILE GL.TRANS.CODES
* DB_AMT  - DEBIT AMOUNT
* CR_AMT  - CREDIT AMOUNT
* EFF_DATE - EFFECTIVE DATE FOR GL ENTRY
* SUBSYS - SUB SYSTEM: AR-INV - SALES INVOICE REF = SALES INVOICE NUMBER * PACKSLIP / SA ID FOR LINE ITEM
*                      AP-INV - ACCOUNTS PAYABLE INVOICE REF = VENDOR ID*INVOICE NBR
*                      AR-CHK - ACCOUNTS RECEIVABLE CHECK REF = CUSTOMER ID * CHECK NBR
*                      AP-CHK - ACCOUNTS PAYABLE CHECK REF = BANK CODE * CHECK NBR
* REF_ID - SEE ABOVE
*
* FILES:
*
*
* DESIGN DESCRIPTION:
* ROUTINE IS CALLED TO WRITE A GLT RECORD
* CURRENT DESIGN OF GLT FILE IS TO HAVE 2 ENTRIES FOR EACH PASSED TRANSACTION
* 1 TO THE DB ACCOUNT
* 1 TO THE CR ACCOUNT
* REASON FOR THIS IS WE CURRENTLY ARE NOT IMPLYMENTING A FULL BLOWN GL SYSTEM
* BUT WE WILL HAVE A FILE THAT IS EASILY SENT TO AN EXCEL SPREAD SHEET THAT SHOULD END UP IN BALANCE
*
!
** INCLUDE STANDARD VARIABLE, EQUATES
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES GLT.H
$INCLUDE NXT.INCLUDES VALIDATION_FILE.H
*
$INCLUDE GUIBP ATGUIEQUATES
*
PROG_ID = "WRITE.GLT"
GET_REC_MSG_LIST = ''
*
GLT_REC = ''
*
CALL GET.RECORD("VALIDATION_FILE",C.VALIDATION_FILE,"GL.TRANS.CODES",GLC_REC,@FALSE,ERROR_STATUS,GET_REC_MSG_LIST,GUIERRORS,GUISTATE)
IF GLC_REC = '' THEN
  LOG.OPTS  = "DISPLAY"
  ERROR_NBR = ERR_SYS
  ERROR_MSG =  "Missing GL.TRANS.CODES from VALIDATION_FILE"
  CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
  STATUS = ERROR_MSG
END ELSE
  *
  LOCATE GL_CODE IN GLC_REC<1,1> BY "AL" SETTING IDX THEN
    UNIQUE_ID = SYSTEM(19)
    GL_DB_ACCT =  GLC_REC<CODE_VAL1,IDX>
    GL_CR_ACCT =  GLC_REC<CODE_VAL2,IDX>
    GLT_REC<GL_DB_AMT> = 0
    GLT_REC<GL_CR_AMT> = 0
    GLT_REC<GL_DATE> = EFF_DATE
    GLT_REC<GL_SUB_SYS> =SUBSYS
    GLT_REC<GL_REF> =REF_ID
    IF DB_AMT GE 0 THEN
      *
      * DB ENTRY
      *
      GLT_REC<GL_DB_AMT> = DB_AMT
      GLT_REC<GL_CR_AMT> = 0
      GLT_ID = GL_DB_ACCT:'*':UNIQUE_ID
      WRITE GLT_REC ON C.FILE.VAR(C.GLT), GLT_ID
      *
      * CR ENTRY
      *
      GLT_REC<GL_DB_AMT> = 0
      GLT_REC<GL_CR_AMT> = CR_AMT
      GLT_ID = GL_CR_ACCT:'*':UNIQUE_ID
      WRITE GLT_REC ON C.FILE.VAR(C.GLT), GLT_ID
      *
    END ELSE  ;* REVERSE ENTRY
      *
      * DB ENTRY
      *
      GLT_REC<GL_DB_AMT> = 0
      GLT_REC<GL_CR_AMT> = DB_AMT * -1
      GLT_ID = GL_DB_ACCT:'*':UNIQUE_ID
      WRITE GLT_REC ON C.FILE.VAR(C.GLT), GLT_ID
      *
      * CR ENTRY
      *
      GLT_REC<GL_DB_AMT> = CR_AMT * -1
      GLT_REC<GL_CR_AMT> = 0
      GLT_ID = GL_CR_ACCT:'*':UNIQUE_ID
      WRITE GLT_REC ON C.FILE.VAR(C.GLT), GLT_ID
    END
    *
  END ELSE
    LOG.OPTS  = "DISPLAY"
    ERROR_NBR = ERR_SYS
    ERROR_MSG =  "GL TRANS CODE: ":GL_CODE:', NOT FOUND IN GL.TRANS.CODES VALIDATION_FILE'
    CALL LOG.ERROR(LOG.OPTS,PROG_ID, ERROR_NBR, ERROR_MSG, GUIERRORS, GUISTATE)
    STATUS = ERROR_MSG
  END
  *
END
RETURN
END
