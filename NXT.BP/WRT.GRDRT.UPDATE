SUBROUTINE WRT.GRDRT.UPDATE(PN_ID,ENG_ID,SEQ.LIST,CTRLID,GUIAPP,GUIFRM,GUIERRORS,GUISTATE)
!
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: WRT.GRDRT.UPDATE
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 03/11/2013
* UPDATED:
*
* MAINTENANCE RECORD:
*
* EQUATE:
*
*
* VARIABLES:
* PN_ID - PART ID
* ENG_ID - ENGINEERING ID
* SEQ.LIST - DYNAMIC ARRAY CONTAINING DATA TO WRITE TO THE GRID
* SEQ.LIST<1,X,1> - X = LINE NUMBER 1 = COLUMN FOR SEQ ID
* SEQ.LIST<1,X,2> - X = LINE NUMBER 2 = COLUMN FOR WC
* SEQ.LIST<1,X,3> - X = LINE NUMBER 3 = COLUMN FOR WC NAME
* SEQ.LIST<1,X,4> - X = LINE NUMBER 4 = COLUMN FOR SEQ FIRST LINE OF DESC

* CTRLID - CONTROL ID (USUALLY GRDRT)
*
* FILES:
*
*
* DESIGN DESCRIPTION:
* ROUTINE IS CALLED UPDATE THE GRID CONTAINING THE RT SEQ IF AND THE LIST VARIABLE SEQ.LIST
*
!
** INCLUDE STANDARD VARIABLE, EQUATES
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES PARTS.H
$INCLUDE NXT.INCLUDES SYSTEM_FILE.EQU
$INCLUDE NXT.INCLUDES WRT.H
$INCLUDE NXT.INCLUDES WC.H
*
$INCLUDE GUIBP ATGUIEQUATES
*
PROG_ID   = "WRT.GRDRT.UPDATE"
GUIERRORS = ''
*
* FIRST CLEAR OUT SEQ LIST
*
MSG_LIST = ""
SEQ.LIST = ''
IDX = 0
*
* NOW GET OUR SEQ DATA
SLIST = 'SSELECT RT BY PARTNBR BY ENGID BY SEQNBR WITH PARTNBR = "':PN_ID:'" AND WITH ENGID = "':ENG_ID:'" TO 2'
EXECUTE SLIST CAPTURING DMMY.OUT
*
IF @SELECTED GE 1 THEN
  READLIST IDLIST FROM 2 THEN
    LIST.CT = DCOUNT(IDLIST,@AM)
    FOR I = 1 TO LIST.CT
      RT_ID = IDLIST<I,1>
      IDX += 1
      SEQ.LIST<1,IDX,1> = FIELD(RT_ID,"*",3)   ;* SEQ NUMBER
      CALL GET.RECORD("RT",C.RT,RT_ID,RT_REC,@FALSE,ERROR_STATUS,MSG_LIST,GUIERRORS,GUISTATE)
      IF ERROR_STATUS = '' THEN
        WC_ID = RT_REC<WRT_WCID>     ;* WORK CENTER
        SEQ.LIST<1,IDX,2> = WC_ID
        CALL GET.RECORD("WC",C.WC,WC_ID,WC_REC,@FALSE,ERROR_STATUS,MSG_LIST,GUIERRORS,GUISTATE)
        IF ERROR_STATUS = '' THEN
          WCNAME = WC_REC<WC_NAME>
        END ELSE
          WCNAME = 'Unknown!'
        END
        SEQ.LIST<1,IDX,3> = WCNAME
        SEQ.LIST<1,IDX,4> = RT_REC<WRT_DESC,1>
      END ELSE
        SEQ.LIST<1,IDX,2> = 'UNKNOWN'
        SEQ.LIST<1,IDX,3> = 'UNKNOWN'
        SEQ.LIST<1,IDX,4> = 'RT RECORD MISSING!'
      END
    NEXT I
  END
END
*
IF CTRLID # '' THEN
  CALL ATGUISETPROPS(GUIAPP,GUIFRM,CTRLID,GPVALUE,SEQ.LIST,GUIERRORS,GUISTATE)
  IF GUIERRORS<1> >= NXT.GUI.ERROR.LEVEL THEN CALL LOG.ERROR("DISPLAY",PROG_ID, ERR_GUI, ERR_MESSAGE(ERR_GUI), GUIERRORS, GUISTATE)
END

RETURN
END
