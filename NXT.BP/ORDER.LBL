SUBROUTINE ORDER.LBL(OP_REC,OC_REC,OC_ID,CUST_NAME,PRINTER_NBR,GUIERRORS,GUISTATE)

**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: ORDER.LBL
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 00/00/2009
* UPDATED:
*
* MAINTENANCE RECORD:
* 01/13/21 - DO NOT ALLOW PRINTING ON UNIT 0 (WE ASSUME DEFAULT PRINTER IS NO A LABEL PRINTER!)
*
* EQUATE:
*
*
* VARIABLES:
* RETURNS:
* OC_REC - ORDER CHILD REC FOR ORDER TO PRINT PROCESS LABEL
* OC_ID  - ORDER CHILD ID
* CUST_NAME - CUSTOMER NAME
* CUST_PO_NBR - CUSTOMER PO NUMBER OF ORDER
* PRINTER_NBR - PRINTER UNIT NUMBER TO PRINT LABEL ON
* GUIERRORS, GUISTATE - ACCUTERM GUI VARIABLES, PASSED FOR ERROR REPORTING
*
*
* FILES:
*
*
* DESIGN DESCRIPTION:
* OUTPUT PROCESS LABEL
* Note Routine assume a label printer than understands  Datamax-O'Neil Programming Language (DPL).
*
** INCLUDE STANDARD VARIABLE, EQUATES
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.PRT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
$INCLUDE NXT.INCLUDES SYSTEM_FILE.EQU
$INCLUDE NXT.INCLUDES ORDER_CHILD.H
$INCLUDE NXT.INCLUDES ORDER_PARENT.H
$INCLUDE NXT.INCLUDES PARTS.H
*
IF PRINTER_NBR = 0 THEN
   PRMPT = "Label Printer Not Setup For This User"
   CAPTION = "Order Label Print"
   STYLE   = 2  ;* !
   BUTTONS = 0  ;* ok only
   HELPID = ""
   CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
END ELSE
   *
   * GET THE SPC CODES AND DESCRIPTIONS
   * SPC.IDX = AM LIST OF SALES CODES
   * SPC.LIST = VM LIST OF SALES CODE AND DESC
   *             SALES.CODE:SVM:DESC:VM:SALES.CODE:SVM:DESC .......
   *
   SPC.LIST = ''
   SPC.IDX = ''
   CALL LIST.LOAD(C.VALIDATION_FILE,"SALES.CODES","2",'',SPC.LIST,SPC.IDX,LIST.COUNT,EMSG)
   *
   *
   * PULL OFF SALES PRODUCT CODE AND DESC, ADD TO ROUTER
   * (NOTE DFLT = "STANDARD"
   SPC_ID = OC_REC<OC_SPC>     ;* SALES PRODUCT CODE
   IF SPC_ID = '' THEN
      SPC_DESC = "-- STANDARD - ORDER"
   END ELSE
      LOCATE SPC_ID IN SPC.IDX<1> SETTING IDX THEN
         SPC_DESC  = "-- ":SPC.LIST<1,IDX,2>:" - ORDER"
      END ELSE
         SPC_DESC  = "-- ":SPC_ID:'-':"Nof"
      END
   END
   *
   *ROW = 275     ;* OFFSET FROM HOME POSITION OF LABEL, WHICH IS LOWER LEFT CORNER
   ROW = 245      ;* DROP DOWN MORE 275 WAS NOT PRINTING ALL OF "ORDER NBR" ON LABEL
   *             ;* REM WE PRINT FROM TOP TO BOTTOM
   ROW_DEC = 15  ;* (little more than 1/8 inch)
   GET_REC_MSG_LIST = ''
   *
   * PULL INFO FROM THE ORDER CHILD RECORD
   *
   DEL_QTY = 0
   DEL.CNT = DCOUNT(OC_REC<OC_ORD_QTY>,@VM)
   FOR I = 1 TO DEL.CNT
      DEL_QTY += OC_REC<OC_ORD_QTY,I>
   NEXT I
   PROCESS_ID   = OC_REC<OC_PROCESS_ID>
   PROCESS_DESC = OC_REC<OC_PROCESS_DESC>
   PROCESS_CERT = OC_REC<OC_PROCESS_CERT>
   PART_DESC    = OC_REC<OC_PART_DESC>
   PART_REV     = OC_REC<OC_REV>
   PART_DRAW    = OC_REC<OC_DRAWING>
   PART_NBR     = OC_REC<OC_PART_NBR>
   PART_MTL     = OC_REC<OC_MATL>
   NOTES        = OC_REC<OC_ITEM_NOTES>
   ORDER_DATE   = OCONV(OP_REC<OP_DATE>,OP_DATE.CNV)
   *
   * OUTPUT THE LABEL
   *
   OUT.DATA = STX:"L":CR   ;* ENTER LABEL FORMAT MODE
   OUT.DATA := "D11":CR     ;* 300 DPI
   *
   OUT.DATA := "1511000":ROW"R%4":"0025"
   ;* 1 = ROTATION 0 degs, 3 = font #3
   ;* 11 HOZT & WDTH MULT
   ;* 000 n/a POINT FONT
   ;* 00275 ROW (1/10 of an inch inc or 2.75 from home "bottom" of label)
   ;* 0025 COLMN POSTION (.25 inch )

   OUT.DATA := 'Order Nbr.  : ':FIELD(OC_ID,"*",1):CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1511000":ROW"R%4":"0025"
   OUT.DATA := SPC_DESC:CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'Part Number : ':PART_NBR:' rev: ':PART_REV:CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'Description : ':PART_DESC "L#26":CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'Drawing     : ':PART_DRAW "L#26":CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'Material    : ':PART_MTL "L#26":CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'Customer    : ':CUST_NAME"L#26":CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'PO Nbr.     : ':OP_REC<OP_PO_NBR>"L#26":CR
   *
   ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
   OUT.DATA := "1311000":ROW"R%4":"0025"
   OUT.DATA := 'Date: ':ORDER_DATE:CR
   *
   DEL.CNT = DCOUNT(OC_REC<OC_ORD_QTY>,@VM)
   FOR I = 1 TO DEL.CNT
      DEL_QTY = OC_REC<OC_ORD_QTY,I>
      DUE_DATE  = OCONV(OC_REC<OC_DUE_DATE,I>,OC_DUE_DATE.CNV)
      *
      ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
      ROW -= ROW_DEC  ;* POSITION TO NEXT ROW
      OUT.DATA := "1411000":ROW"R%4":"0025"
      OUT.DATA := 'Quantity    : ':DEL_QTY:'  Due:  ':DUE_DATE:CR
      IF ROW LE 20 THEN GOTO OUT_OF_LABEL
   NEXT I
   *
OUT_OF_LABEL: *

   *
   * LABEL DEF COMPLETE
   *
   OUT.DATA := "E":CR
   *
   PRINT ON PRINTER_NBR OUT.DATA
   PRINTER CLOSE ON PRINTER_NBR
END
*
9999: *
RETURN
END

