SUBROUTINE GET.PARTS(PN_ID,PARTS_REC,LOCK_PN,NEW_PN,ERROR_STATUS,MSG_LIST,GUIERRORS,GUISTATE)
!
**************************************************************************
* This is free and unencumbered software released into the public domain.
*
* Anyone is free to copy, modify, publish, use, compile, sell, or
* distribute this software, either in source code form or as a compiled
* binary, for any purpose, commercial or non-commercial, and by any
* means.
*
* In jurisdictions that recognize copyright laws, the author or authors
* of this software dedicate any and all copyright interest in the
* software to the public domain. We make this dedication for the benefit
* of the public at large and to the detriment of our heirs and
* successors. We intend this dedication to be an overt act of
* relinquishment in perpetuity of all present and future rights to this
* software under copyright law.
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
* EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
* MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
* IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY CLAIM, DAMAGES OR
* OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE,
* ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR
* OTHER DEALINGS IN THE SOFTWARE.
*
* For more information, please refer to <https://unlicense.org>
**************************************************************************
*
* MODULE: GET.PARTS
* AUTHOR: MAB
* VERSION: 1.0.0
* CREATED: 00/00/2007
* UPDATED:
*
* MAINTENANCE RECORD:
*
* EQUATE:
*
*
* VARIABLES:
* PN_ID = PARTS RECORD TO ACCESS
* PARTS_REC - READ RECORD OR NULL IF RECORD NOT FOUND
* LOCK_PN - SET LOCK PROIR TO READ (T/F)
* NEW_PN  - RETURNED FLAG (T/F)
* ERROR_STATUS - SET TO ERR-NOF IF PN_ID NOT FOUND
* MSG_LIST <1> - UNABLE TO LOCK MESSAGE
* MSG_LIST <2> - USER OK'D LOCK SKIP
*
* FILES:
*
*
* DESIGN DESCRIPTION:
* ROUTINE IS CALLED TO ACCESS RECORD
* IF LOCK_REC THEN LOCK  RECORD GET_ID
* IF NOT LOCKSET THEN ERROR_STATUS = ERR_BUSY
* READ REC ELSE
*   GET_REC = ''
*   IF NULL ERROR_STATUS THEN ERROR_STATUS = ERR_NOF
*
!
** INCLUDE STANDARD VARIABLE, EQUATES
*
$INCLUDE NXT.INCLUDES NXT.COMMON
$INCLUDE NXT.INCLUDES NXT.EQUATES
$INCLUDE NXT.INCLUDES NXT.ERR.MSGS
*
$INCLUDE GUIBP ATGUIEQUATES
*
ERROR_STATUS = ''
NEW_PN = @FALSE
*
*IF MSG_LIST = '' THEN
MSG_LIST<1> = "Unable to Update Lock: ":PN_ID:"-":@SVM:"Click OK to Proceed Without Locking Record,":@SVM:"Cancel to Reattempt Lock"
MSG_LIST<2> = "Record not Locked,":@SVM:"Updates Not Allowed!"
*END
*
TRY.AGAIN:
CALL GET.RECORD("PARTS",C.PARTS,PN_ID,PARTS_REC,@FALSE,ERROR_STATUS,MSG_LIST,GUIERRORS,GUISTATE)
IF ERROR_STATUS = ERR_NOF OR PARTS_REC = '' THEN
  *
  * USER ENTERED PART WAS NOT FOUND, DO WE WANT TO ADD THIS AS A NEW PART OR SEARCH?
  *
  PRMPT = "Part number: ":PN_ID:" is not on file,":@SVM: "Search for part number matching : ":PN_ID:"?"
  CAPTION = "Part Get"
  STYLE   = 0  ;* no icon
  BUTTONS = 4  ;* yes / no
  HELPID = ""
  CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
  IF RESPONSE = 6 THEN  ;* yes, search for part
    PN_TO_SEARCH = PN_ID
    CALL PN.SEARCH(PN_TO_SEARCH,PN_SELECTED,GUIERRORS,GUISTATE)
    IF PN_SELECTED = '' THEN   ;* NONE FOUND OR SELECTED
      PRMPT = "No part number found"
      CAPTION = "Part Get"
      STYLE   = 2  ;* !
      BUTTONS = 0  ;* ok only
      HELPID = ""
      CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
      ERROR_STATUS = ERR_NOF
    END ELSE
      PN_ID = PN_SELECTED
      GOTO TRY.AGAIN
    END
  END ELSE
    *     ASSUME RESPONSE IS NO, ADD PART?
    PRMPT = "Part number: ":PN_ID:" is not on file,":@SVM:"Do you wish to create a new part number?"
    CAPTION = "Part Get"
    STYLE   = 0  ;* no icon
    BUTTONS = 4  ;* yes / no
    HELPID = ""
    CALL NXT.ERR.MSG(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, GUIERRORS,GUISTATE)
    IF RESPONSE = 6 THEN  ;* yes, add new part
      NEW_PN = @TRUE
      CALL PN.DIALOG(PN_ID,PARTS_REC,@FALSE,GUIERRORS,GUISTATE)
    END ELSE
      ERROR_STATUS = ERR_NOF  ;* NOT ON FILE, USER DID NOT WANT TO ADD, AND USER DID NOT WANT TO SEARCH
    END
  END
END ;*  PART FOUND, RETURN TO CALLER
RETURN
END
